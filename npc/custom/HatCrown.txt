// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "crownqshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
gonryun,211,123,4	script	Joaillier Ambulant#1	4_M_MANAGER,{ callfunc "crownqshop"; }


// Script Core
//============================================================
-	script	quest_shop#crown	-1,{ 
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,0;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Hat";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5589,1,10000000,0,2234,1,729,30,993,50,976,100,2298,5,511,300,12065,3,707,500);
	Add(1,5588,1,10000000,0,2235,1,729,30,993,50,976,100,2298,5,511,300,12065,3,707,500);
	Add(1,5740,1,10000000,0,2234,1,723,30,990,50,975,100,7122,300,12050,3,507,300,1068,500);
	Add(1,5739,1,10000000,0,2235,1,723,30,990,50,975,100,7122,300,12050,3,507,300,1068,500);
	Add(1,5512,1,10000000,0,2234,1,726,30,991,50,978,100,951,300,12000,10,5014,5,995,100);
	Add(1,5513,1,10000000,0,2235,1,726,30,991,50,978,100,951,300,12000,10,5014,5,995,100);
	Add(1,5549,1,10000000,0,2234,1,724,30,990,50,983,100,1021,300,7107,300,947,300,6032,300);
	Add(1,5550,1,10000000,0,2235,1,724,30,990,50,983,100,1021,300,7107,300,947,300,6032,300);
	Add(1,5677,1,10000000,0,2234,1,725,30,990,50,975,100,7264,300,7041,300,678,50,7322,300);
	Add(1,5676,1,10000000,0,2235,1,725,30,990,50,975,100,7264,300,7041,300,678,50,7322,300);
	Add(1,5581,1,10000000,0,2234,1,718,30,990,50,980,100,994,300,7097,300,1914,5,697,100);
	Add(1,5582,1,10000000,0,2235,1,718,30,990,50,980,100,994,300,7097,300,1914,5,697,100);
	Add(1,5545,1,10000000,0,2234,1,728,30,976,100,7120,300,7043,300,909,300,6033,7);
	Add(1,5546,1,10000000,0,2235,1,728,30,976,100,7120,300,7043,300,909,300,6033,7);
	Add(1,5599,1,10000000,0,2234,1,727,100,991,50,978,100,1034,300,509,300,746,300,7003,300);
	Add(1,5598,1,10000000,0,2235,1,727,100,991,50,978,100,1034,300,509,300,746,300,7003,300);
	Add(1,5745,1,10000000,0,2234,1,719,30,990,50,981,100,7198,300,12346,20,911,300,943,300);
	Add(1,5744,1,10000000,0,2235,1,719,30,990,50,981,100,7198,300,12346,20,911,300,943,300);
	Add(1,5663,1,10000000,0,2234,1,721,30,979,100,521,300,7298,300,936,300,7186,300);
	Add(1,5662,1,10000000,0,2235,1,721,30,979,100,521,300,7298,300,936,300,7186,300);
	Add(1,5569,1,10000000,0,2234,1,722,30,993,50,982,100,4354,1,2659,1,2660,1,2661,1,2662,1);
	Add(1,5570,1,10000000,0,2235,1,722,30,993,50,982,100,4354,1,2659,1,2660,1,2661,1,2662,1);
	Add(1,5514,1,10000000,0,2234,1,720,30,991,50,978,100,956,300,510,300,7070,300,965,300);
	Add(1,5515,1,10000000,0,2235,1,720,30,991,50,978,100,956,300,510,300,7070,300,965,300);

	
	//c good
// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "crownqshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "crownqshop"+.@i,1;
	npcshopattach "crownqshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			// specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "crownqshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	crownqshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop#crown::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	crownqshop1	-1,909:-1
-	shop	crownqshop2	-1,909:-1
-	shop	crownqshop3	-1,909:-1
-	shop	crownqshop4	-1,909:-1
-	shop	crownqshop5	-1,909:-1