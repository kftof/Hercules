// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "girlqshop"{,<shop ID>{,<shop ID>{,...}}};
//============================================================
gonryun,205,138,4	script	Hat Girl#1	4_F_LGTGIRL,{ callfunc "girlqshop"; }


// Script Core
//============================================================
-	script	quest_shop#11	-1,{ 
function Add; function Chk; function Slot; function A_An;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,0;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,0;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],"#CASHPOINTS","Cash Points";

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],"Hat";

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

	Add(1,5592,1,0,0,949,300,7063,200,7441,300,2255,1,2254,1,4316,5,1737,1,7064,500);
	Add(1,29358,1,0,0,5592,1,976,100,973,20);
	Add(1,5132,1,0,0,5035,1,2254,3,2289,1,523,100);
	Add(1,5082,1,0,0,921,300);
	Add(1,5021,1,0,0,2233,1,949,80,969,1,999,20,938,800);
	Add(1,5187,1,0,0,5085,1,2244,2,2209,3,978,15,975,15);
	Add(1,5020,1,0,0,10007,1,10009,1);
	Add(1,5277,1,0,0,6110,1,993,100,7448,100,7149,100,7452,1);
	Add(1,5254,1,0,0,1045,500,2255,1,1038,300,983,25);
	Add(1,5200,1,0,0,978,20,982,20,994,100,7213,300,7563,50);
	Add(1,5032,1,0,0,5062,1,2227,1,1059,250,2607,1,7063,600);
	Add(1,5043,1,0,0,1048,50,2281,1);
	Add(1,29115,1,0,0,5062,2,5117,1);
	Add(1,29291,1,0,0,5074,5,923,200);
	Add(1,29292,1,0,0,5068,5,923,200);
	Add(1,5140,1,0,0,10007,25,2250,1,5083,1);
	Add(1,18539,1,4000000,0,7752,300,7005,300,983,50,7799,100,7315,100);
// NOUVEAUX HATSX
	Add(1,29327,1,0,0,2227,1,914,500,12091,1,12083,1,7115,50,7063,150);
	Add(1,29330,1,0,0,2227,1,914,500,12062,1,12091,1,7107,200,947,150);
	Add(1,29331,1,0,0,2227,1,632,7,12066,1,4249,1,623,4);
	Add(1,29332,1,0,0,2227,1,914,500,12257,1,1275,1,995,30,1008,1,7063,300);
	Add(1,29333,1,0,0,2227,1,2310,10,7065,300);
	Add(1,29334,1,0,0,2227,1,914,250,2256,1,7107,200,7166,100,2322,3);
	Add(1,29384,1,0,0,736,10,7206,100,727,30,561,3,982,25,5368,1,2502,1);
	Add(1,29351,1,0,0,2502,1,7206,100,718,30,7216,200,7456,1,973,5,975,50,722,10);
	Add(1,29356,1,0,0,7445,250,7446,250,5398,1,7266,100,12080,10,4385,3,1037,250,12084,1);
	Add(1,29359,1,0,0,5160,1,2229,1,7023,40,7799,100,5808,1,7315,200,4169,1,7157,150);
	Add(1,29429,1,0,0,7315,200,7023,5,2209,5,7174,25,2250,2,7161,150,1003,250,983,50);
	Add(1,29360,1,0,0,736,1,7751,150,727,20,5170,1,982,30);
	Add(1,29361,1,0,0,7754,1,2235,1,732,3,719,3,720,3,724,3,721,3,718,3,723,3,726,3,725,3,728,3,729,3,7435,25,5002,1);
	Add(1,29366,1,0,0,2209,3,973,3,12055,1,7568,100);
	Add(1,29367,1,0,0,12052,5,1973,2,951,250,721,40,2229,1,969,30,988,1,991,25,960,250,12054,1);
	Add(1,29369,1,0,0,1019,150,7150,150,4375,1,7298,150);
	Add(1,29370,1,0,0,2286,10,2622,2,7109,5);
	Add(1,29378,1,0,0,2287,3,12074,2,1059,300,993,30,7448,250);
	Add(1,29379,1,0,0,539,200,7115,100,7563,100,5074,2,1246,1,7166,200);
	Add(1,29382,1,0,0,4023,1,4082,1,1148,1,4029,1,4415,1,722,15,702,50);
	Add(1,29383,1,0,0,2235,1,7213,250,12099,1,988,1,10016,1,1060,300,7435,25);
	Add(1,29461,1,0,0,2213,1,5081,1,13106,1,988,1,10016,1,7435,10);


// -----------------------------------------------------------

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "girlqshop"+.@i,909;
	}
	end;

OnMenu:
	set .@size, getarraysize(@i);
	if (!.@size) set .@i, select(.menu$);
	else if (.@size == 1) set .@i, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set .@i, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[.@i] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "Select one item at a time.";
	callshop "girlqshop"+.@i,1;
	npcshopattach "girlqshop"+.@i;
	end;

OnBuyItem:
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[Quest Shop]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0],5), getiteminfo(.@q[0],11);
	if (@qe[2] > 0 && ((@qe[1] & 1) || (@qe[1] & 256) || (@qe[1] & 512) || (@qe[1] & 1024) || (@qe[1] & 2048) || (@qe[1] & 4096) || (@qe[1] & 4) || (@qe[1] & 8192)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) { 
				mes "[Quest Shop]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[Quest Shop]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0],6))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):A_An(getitemname(.@q[0])))+"!",0;
			// specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "girlqshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0),2) == 4 || getiteminfo(getarg(0),2) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}

function A_An {
	setarray .@A$[0],"a","e","i","o","u";
	set .@B$, "_"+getarg(0);
	for(set .@i,0; .@i<5; set .@i,.@i+1)
		if (compare(.@B$,"_"+.@A$[.@i])) return "an "+getarg(0);
	return "a "+getarg(0);
}
}

function	script	girlqshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "quest_shop#11::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	girlqshop1	-1,909:-1
-	shop	girlqshop2	-1,909:-1
-	shop	girlqshop3	-1,909:-1
-	shop	girlqshop4	-1,909:-1
-	shop	girlqshop5	-1,909:-1