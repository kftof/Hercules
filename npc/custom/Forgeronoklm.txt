//===== rAthena Script =======================================
//= Advanced Refiner
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.6
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= [Official Conversion]
//= Refiner that uses Enriched ores to increase upgrade success.
//= After a conversation with Doddler, it's been established that
//= the advanced refiner works similar the the "Bubble Gum" item.
//= The success percentage is not "increased" however, if it fails
//= You get a second try. This tries twice at the same time,
//= effectively giving you a re-roll on your attempt.
//= - Dialog is only partly official to iRO.
//= - Uses the iRO position for this NPC.
//===== Additional Comments: =================================
//= 1.0 First Version. [L0ne_W0lf]
//= 1.1 Fixed a weird carriage return. o_o [L0ne_W0lf]
//= 1.2 Optimizing refine method [Zephyrus]
//= 1.3 Typo fixes [Yommy]
//= 1.4 Removed unnecessary dialogs [Zephyrus]
//= 1.4a Added 'disable_items' command. [Euphy]
//= 1.4b Fixed coordinates. [Euphy]
//= 1.5 Some official script updates. [Euphy]
//= 1.6 Added VIP features. [Euphy]
//============================================================

gon_in,93,11,3	script	Enriched Blacksmith	1_M_SMITH,{
	disable_items;
	mes "[Suhnbi]";
	mes "J'ai le don de travailler les minerais enrichies.";
	mes "Je peux upgrade tes armes et tes armures.";
	mes "Indique moi ce que tu souhaites que je travaille.";
	next;

	setarray .@position$[1], "Tete Haut","Armure","Main Gauche","Main Droite","Cape","Chaussures","Accessoire Gauche","Accessoire Droit","Tete Milieu","Tete Bas";
	set .@menu$,"";
	for(set .@i,1; .@i<=10; set .@i,.@i+1) {
		if (getequipisequiped(.@i)) {
			set .@menu$, .@menu$ + .@position$[.@i] + "-" + "[" + getequipname(.@i) + "]";
			set .@equipped,1;
		}
		set .@menu$, .@menu$ + ":";
	}
	if (.@equipped == 0) {
		mes "[Suhnbi]";
		mes "Je ne peux pas t'aider.";
		close;
	}
	set .@part, select(.@menu$);

	if (!getequipisequiped(.@part)) //custom check
		close;
	if (!getequipisenableref(.@part)) {
		mes "[Suhnbi]";
		mes "Je ne peux pas améliorer cet objet.";
		close;
	}
	if (getequiprefinerycnt(.@part) >= 10) {
		mes "[Suhnbi]";
		mes "Je ne peux plus améliorer cet objet.";
		close;
	}

	// Make sure you have the neccessary items and Zeny to refine your items
	// Determines chance of failure and verifies that you want to continue.
	switch(getequipweaponlv(.@part)) {
		case 1: callsub S_RefineValidate,1,7620,1,.@part; break;
		case 2: callsub S_RefineValidate,2,7620,1,.@part; break;
		case 3: callsub S_RefineValidate,3,7620,1,.@part; break;
		case 4: callsub S_RefineValidate,4,7620,1,.@part; break;
		default: callsub S_RefineValidate,0,7619,1,.@part; break;
	}

	mes "[Suhnbi]";
	mes "Clang! Clang! Clang!";
	if (getequippercentrefinery(.@part) > rand(100) || getequippercentrefinery(.@part) > rand(100)) {
		successrefitem .@part;
		next;
		emotion e_no1;
		mes "[Suhnbi]";
		mes "Voila, c'est fait !";
		close;
	}
	failedrefitem .@part;
	next;
	emotion (!rand(5))?e_cash:e_omg;
	mes "[Suhnbi]";
	mes "Wouups.";
	next;
	mes "[Suhnbi]";
	mes "...";
	mes "C'est un echec.";
	close;

S_RefineValidate:
	set .@price, getarg(2);
/*
	// If the VIP system is enabled, the prices for non-VIP players are considerably higher.
	if (VIP_SCRIPT && !vip_status(1)) {
		switch(getarg(0)){
			case 0: set .@price, .@price * 10; break;
			case 1: set .@price, .@price * 40; break;
			case 2: set .@price, .@price * 50; break;
			case 3: set .@price, .@price * 2; break;
			case 4: set .@price, .@price * 2; break;
		}
	}
*/
	mes "[Suhnbi]";
	if (getarg(0))
		mes "Tu veux optimiser une arme de niveau " + getarg(0) + " ?";
	mes "Je vais avoir besoin d'un ^ff9999" + getitemname(getarg(1)) + "^000000 ainsi que de " + .@price + " zeny.";
	mes "Veux-tu continuer ?";
	next;
	if(select("Oui:Non") == 1) {
		if (getequippercentrefinery(getarg(3)) < 100) {
			if (getarg(0)) {
				mes "[Suhnbi]";
				mes "Hmmm.";
				mes "Il y a une chance que je casse ton item si je l'upgrade encore.";
				next;
				mes "Si il casse, tu ne pourras plus l'utiliser.";
				mes "Es-tu certain de vouloir continuer ?";
				next;
				if(select("Oui:Non") == 2) {
					mes "[Suhnbi]";
					mes "Bien.";
					close;
				}
			} else {
				mes "[Suhnbi]";
				mes "Tu as du cran.";
				mes "Es-tu vraiment certain de toi ?";
				next;
				if(select("Oui:Non") == 2) {
					mes "[Suhnbi]";
					mes "Tu me fais mon temps.";
					close;
				}
			}
		}
		if (countitem(getarg(1)) > 0 && Zeny > .@price) {
			delitem getarg(1),1;
			set Zeny, Zeny - .@price;
			return;
		}
		mes "[Suhnbi]";
		mes "C'est tout ce que tu as ?";
		mes "Il va me falloir des matériaux, je ne travaille pas gratuitement.";
		close;
	}
	mes "[Suhnbi]";
	mes "Je ne peux rien faire pour toi.";
	close;
}