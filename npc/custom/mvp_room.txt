//===== rAthena Script =======================================
//= Private MVP & Branch Room
//===== By: ==================================================
//= AnnieRuru
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= Allows players to rent an MVP room for personal use,
//= or for a party or guild.
//===== Additional Comments: =================================
//= 1.0 First version, edited. [Euphy]
//============================================================

gonryun,156,131,4	script	Private MVP/DB Room	4_MAN_GALTUN1,{
	mes "[Private MVP/DB Room]";
	mes "Veuillez choisir une MvP Room.";
	next;
	if ( getvariableofnpc( .rentcost, "MVP Summoner" ) )
		mes "Le prix pour réserver une salle pour "+ getvariableofnpc( .timeout, "MVP Summoner" ) +" minutes is "+ callfunc("F_InsertComma", getvariableofnpc( .rentcost, "MVP Summoner" ) ) +" zeny.";
	else
		mes "Vous pouvez utiliser la salle pour "+ getvariableofnpc( .timeout, "MVP Summoner" ) +" minutes.";
	mes " ";
	for ( .@i = 1; .@i <= 8; .@i++ )
		if ( getvariableofnpc( .renttime[.@i], "MVP Summoner" ) )
			mes "Room #"+ .@i +" = "+ .color$[ .type[.@i] ] + .whoinuse$[.@i] +"^000000";
	next;
	.@room = select(
		"MVP Room 1 ["+ getmapusers("06guild_01") +"]",
		"MVP Room 2 ["+ getmapusers("06guild_02") +"]",
		"MVP Room 3 ["+ getmapusers("06guild_03") +"]",
		"MVP Room 4 ["+ getmapusers("06guild_04") +"]",
		"MVP Room 5 ["+ getmapusers("06guild_05") +"]",
		"MVP Room 6 ["+ getmapusers("06guild_06") +"]",
		"MVP Room 7 ["+ getmapusers("06guild_07") +"]",
		"MVP Room 8 ["+ getmapusers("06guild_08") +"]");
	if ( getvariableofnpc( .renttime[.@room], "MVP Summoner" ) ) {
		if ( .inuseid[.@room] == getcharid( .type[.@room] ) ) {
			warp "06guild_0"+ .@room, 0,0;
			close;
		} else {
			mes "[Private MVP/DB Room]";
			mes "Cette salle est prévue pour ";
			mes .color$[ .type[.@room] ] + .whoinuse$[.@room] +"^000000.";
			mes "Merci d'en choisir une autre.";
			close;
		}
	}
	mes "[Private MVP/DB Room]";
	mes "Réserver cette salle pour...";
	next;
	set .@type, select( "Pour mon groupe", "Pour ma Guild", "Pour un usage personnel" );
	if ( !getcharid(.@type) ) {
		mes "[Private MVP/DB Room]";
		mes "Vous n'avez pas de "+( ( .@type == 1 )? "Groupe" : "Guild" )+".";
		close;
	}
	else if ( Zeny < getvariableofnpc( .rentcost, "MVP Summoner" ) ) {
		mes "Vous n'avez pas assez de zeny pour réserver une salle.";
		close;
	}
	else if ( getvariableofnpc( .renttime[.@room], "MVP Summoner" ) ) {
		mes "[Private MVP/DB Room]";
		mes "Cette salle est prise par quelqu'un.";
		close;
	}
	for ( .@i = 1; .@i <= 8; .@i++ ) {
		if ( ( getvariableofnpc( .renttime[.@i], "MVP Summoner" ) ) && .@type == .type[.@i] && getcharid(.@type) == .inuseid[.@i] ) {
			mes "[Private MVP/DB Room]";
			mes "Vous avez déjà une salle#"+ .@i +". Merci de l'utiliser.";
			close;
		}
	}
	set .type[.@room], .@type;
	set .inuseid[.@room], getcharid(.@type);
	set .whoinuse$[.@room], strcharinfo( ( .@type == 3 )? 0 : .@type );
	Zeny -= getvariableofnpc( .rentcost, "MVP Summoner" );
	warp "06guild_0"+ .@room, 0,0;
//	killmonsterall "06guild_0"+ .@room;
	donpcevent "MVP Summoner#"+ .@room +"::OnEnterMap";
	close;
OnInit:
	.color$[1] =  "^EE8800"; // party color
	.color$[2] =  "^70CC11"; // guild color
	.color$[3] =  "^0000FF"; // account color
	end;
}

-	script	MVP Summoner	-1,{
	mes "[MVP Summoner]";
	mes "Time left: " + callfunc( "Time2Str", .renttime[ atoi( strnpcinfo(2) ) ] + .timeout * 60 );
	mes "Bonjour, que puis-je faire pour vous ?";
	next;
	switch ( select(.menu$) ) {
	case 1:
		mes "[MVP Summoner]";
		if ( mobcount( "this", strnpcinfo(3)+"::OnMobDead" ) ) {
			mes "Je ne peux prodiguer de soin tant que la salle n'est pas vide.";
			close;
		}
		sc_end SC_STONE;
		sc_end SC_SLOWDOWN;
		sc_end SC_FREEZE;
		sc_end SC_SLEEP;
		sc_end SC_CURSE;
		sc_end SC_SILENCE;
		sc_end SC_CONFUSION;
		sc_end SC_BLIND;
		sc_end SC_BLEEDING;
		sc_end SC_DECREASEAGI;
		sc_end SC_POISON;
		sc_end SC_HALLUCINATION;
		sc_end SC_STRIPWEAPON;
		sc_end SC_STRIPARMOR;
		sc_end SC_STRIPHELM;
		sc_end SC_STRIPSHIELD;
		sc_end SC_CHANGEUNDEAD;
		sc_end SC_ORCISH;
		sc_end SC_BERSERK;
		sc_end SC_SKE;
		sc_end SC_SWOO;
		sc_end SC_SKA;
		percentheal 100,100;
		specialeffect(EF_HEAL, AREA, playerattached());
		mes "Restez sur vos gardes !";
		close;
	case 2:
		if ( mobcount( "this", strnpcinfo(3)+"::OnMobDead" ) ) {
			mes "[MVP Summoner]";
			mes "Je ne peux rien invoquer tant que la salle n'est pas vide.";
			close;
		} else if ( .mvpcost ) {
			mes "[MVP Summoner]";
			mes "Le prix pour invoquer un mvp est de "+ callfunc( "F_InsertComma", .mvpcost ) +" zeny.";
			next;
			set .@menu, select(.mvpid_menu$) -1;
			mes "[MVP Summoner]";
			if ( Zeny < .mvpcost ) {
				mes "Vous n'avez pas assez de zeny pour invoquer un MvP.";
				close;
			}
		} else {
			set .@menu, select(.mvpid_menu$) -1;
			mes "[MVP Summoner]";
		}
		mes "Tenez-vous pret.";
		close2;
		if ( Zeny < .mvpcost ) end;
		if ( mobcount( "this", strnpcinfo(3)+"::OnMobDead" ) ) 
		{
			mes "[MVP Summoner]";
			mes "Je ne peux rien invoquer tant que la salle n'est pas vide.";
			close;
			end;
		}
		Zeny -= .mvpcost;
		monster "this", 0, 0, "--ja--", .mvpid[.@menu], 1, strnpcinfo(3)+"::OnMobDead";
		end;
	case 3:
		if ( mobcount( "this", strnpcinfo(3)+"::OnMobDead" ) ) {
			mes "[MVP Summoner]";
			mes "Je ne peux rien invoquer tant que la salle n'est pas vide.";
			close;
		} else if ( .bosscost ) {
			mes "[MVP Summoner]";
			mes "The cost to summon a special mvp is "+ callfunc( "F_InsertComma", .bosscost ) +" zeny.";
			next;
			set .@menu, select(.bossid_menu$) -1;
			mes "[MVP Summoner]";
			if ( Zeny < .bosscost ) {
				mes "You doesn't have enough zeny to summon a mini-boss.";
				close;
			}
		} else {
			set .@menu, select(.bossid_menu$) -1;
			mes "[MVP Summoner]";
		}
		mes "Please get ready.";
		close2;
		if ( Zeny < .bosscost ) end;
		Zeny -= .bosscost;
		monster "this", 0, 0, "--ja--", .bossid[.@menu], 1, strnpcinfo(3)+"::OnMobDead";
		end;
	case 4:
		if ( mobcount( "this", strnpcinfo(3)+"::OnMobDead" ) > 0 ) {
			mes "[MVP Summoner]";
			mes "I cannot offer this service when there are monsters around.";
			close;
		}
		close2;
		callshop "MVP room#branch", 1;
		end;
	case 5:
		mes "[MVP Summoner]";
		mes "Voulez-vous vraiment partir ?";
		next;
		if ( select( "Yes:No") == 1 )
			warp "gonryun", 159, 124;
		close;
	case 6:
		mes "[MVP Summoner]";
		if ( getmapusers( strcharinfo(3) ) > 1 ) {
			mes "Il y a encore des joueurs dans cette salle.";
			close;
		}
		mes "Etes-vous certain de vouloir abandonner la salle ?";
		if ( .rentcost )
			mes "Vous aurez besoin de payer une nouvelle fois si vous voulez rentrer dans cette salle.";
		next;
		if ( select( "Oui.", "Non." ) == 2 ) close;
		awake strnpcinfo(0);
		end;
	}
	close;

OnMobDead:
	end;

OnEnterMap:
	.@id = atoi( strnpcinfo(2) );
	.renttime[.@id] = gettimetick(2);
	sleep .timeout * 60000;
	mapwarp strnpcinfo(4), "gonryun", 159, 124;
	.renttime[.@id] = 0;
//	killmonsterall strnpcinfo(4);
	end;

OnInit:
	if ( !getstrlen( strnpcinfo(2) ) ) {

//	Config ---------------------------------------------------------------------------------------

	// Room rental time, in minutes.
	// When time runs out, all players inside the room will be kicked out.
	// Do NOT set this to zero!
	set .timeout, 60;

	set .rentcost, 0;	// Zeny cost for renting a room (0 = free)
	set .mvpcost, 3000000;	// Zeny cost to summon an MVP (0 = free)
	set .bosscost, 25000000;	// Zeny cost to summon a boss monster (0 = free)

	//	Options setting inside MVP room
	set .@menu[1], 1;	// Turn Heal option On/Off
	set .@menu[2], 1;	// Turn MVP Summoning On/Off
	set .@menu[3], 1;	// Turn Mini boss Summoning On/Off
	set .@menu[4], 1;	// Sell items (branches) On/Off (see shop below, before the mapflags)

	setarray .mvpid[0],
		1511,//	Amon Ra
		1785,//	Atroce
		1039,//	Baphomet
		1272,//	Dark Lord
		1046,//	Doppelgangger
		1112,//	Drake
		1115,//	Eddga
		1418,//	Evil Snake Lord
		1990,// Hardrock Mammoth		
		1688,//	Lady Tanee
		1373,//	Lord of Death
		1059,//	Mistress
		1150,//	Moonlight Flower
		1087,//	Orc Hero
		1038,//	Osiris
		1157,//	Pharaoh
		1159,//	Phreeoni
		1623,//	RSX 0806
		1492,//	Incantation Samurai
		1251,//	Knight of Windstorm
		1312,//	Turtle General
		1685;// Vesper

	setarray .bossid[0],
		1957,// ENTWEIHEN
		1956;// NAGHT_SIEGER
/*		1388,// Archangeling
		1795,// Bloody Knight
		1830,// Bow Guardian
		1839,// Byorgue
		1309,// Cat O' Nine Tail
		1283,// Chimera
		1302,// Dark Illusion
		1198,// Dark Priest
		1582,// Deviling
		1091,// Dragon Fly
		1093,// Eclipse
		1205,// Executioner
		1783,// Galion
		1592,// Gangster
		1120,// Ghostring
		1259,// Gryphon
		1720,// Hydro
		1090,// Mastering
		1289,// Maya Purple
		1262,// Mutant Dragon
		1203,// Mysteltainn
		1870,// Necromancer
		1295,// Owl Baron
		1829,// Sword Guardian
		1204,// Tirfing
		1089,// Toad
		1092,// Vagabond Wolf
		1765;// Valkyrie
*/

//	Config Ends ------------------------------------------------------------------------

		if ( !.timeout ) set .timeout, 60;
		.menu$ = ( .@menu[1] ? "Heal" : "" ) +":" ;
		.menu$ = .menu$ + ( .@menu[2] ? "Invoquer un MvP" : "" ) +":";
		.menu$ = .menu$ + ( .@menu[3] ? "Invoquer un MvP Spécial" : "" ) +":";
		.menu$ = .menu$ + ( .@menu[4] ? "Acheter des DeadBranch" : "" ) +":";
		.menu$ = .menu$ + "Quitter cette salle.:Abandonner cette salle.";
		if ( .@menu[2] ) {
			.@size = getarraysize( .mvpid );
			for ( .@i = 0; .@i < .@size; .@i++ )
				.mvpid_menu$ = .mvpid_menu$ + getmonsterinfo( .mvpid[.@i], MOB_NAME ) +":";
		}
		if ( .@menu[3] ) {
			.@size = getarraysize( .bossid );
			for ( .@i = 0; .@i < .@size; .@i++ )
				.bossid_menu$ = .bossid_menu$ + getmonsterinfo( .bossid[.@i], MOB_NAME ) +":";
		}
	}
	else {
		mapannounce strnpcinfo(4), "An administrator has refreshed the server. Please re-register this room.", bc_map;
		mapwarp strnpcinfo(4), "gonryun", 159, 124;
	}
	end;
}

-	shop	MVP room#branch	-1,604:5000

06guild_01,49,49,4	duplicate(MVP Summoner)	MVP Summoner#1	4_F_FENRIR
06guild_02,49,49,4	duplicate(MVP Summoner)	MVP Summoner#2	4_F_FENRIR
06guild_03,49,49,4	duplicate(MVP Summoner)	MVP Summoner#3	4_F_FENRIR
06guild_04,49,49,4	duplicate(MVP Summoner)	MVP Summoner#4	4_F_FENRIR
06guild_05,49,49,4	duplicate(MVP Summoner)	MVP Summoner#5	4_F_FENRIR
06guild_06,49,49,4	duplicate(MVP Summoner)	MVP Summoner#6	4_F_FENRIR
06guild_07,49,49,4	duplicate(MVP Summoner)	MVP Summoner#7	4_F_FENRIR
06guild_08,49,49,4	duplicate(MVP Summoner)	MVP Summoner#8	4_F_FENRIR

06guild_01	mapflag	nowarpto
06guild_02	mapflag	nowarpto
06guild_03	mapflag	nowarpto
06guild_04	mapflag	nowarpto
06guild_05	mapflag	nowarpto
06guild_06	mapflag	nowarpto
06guild_07	mapflag	nowarpto
06guild_08	mapflag	nowarpto
06guild_01	mapflag	nomemo
06guild_02	mapflag	nomemo
06guild_03	mapflag	nomemo
06guild_04	mapflag	nomemo
06guild_05	mapflag	nomemo
06guild_06	mapflag	nomemo
06guild_07	mapflag	nomemo
06guild_08	mapflag	nomemo
06guild_01	mapflag	noteleport
06guild_02	mapflag	noteleport
06guild_03	mapflag	noteleport
06guild_04	mapflag	noteleport
06guild_05	mapflag	noteleport
06guild_06	mapflag	noteleport
06guild_07	mapflag	noteleport
06guild_08	mapflag	noteleport
06guild_01	mapflag	nosave	SavePoint
06guild_02	mapflag	nosave	SavePoint
06guild_03	mapflag	nosave	SavePoint
06guild_04	mapflag	nosave	SavePoint
06guild_05	mapflag	nosave	SavePoint
06guild_06	mapflag	nosave	SavePoint
06guild_07	mapflag	nosave	SavePoint
06guild_08	mapflag	nosave	SavePoint
06guild_01	mapflag	nopenalty
06guild_02	mapflag	nopenalty
06guild_03	mapflag	nopenalty
06guild_04	mapflag	nopenalty
06guild_05	mapflag	nopenalty
06guild_06	mapflag	nopenalty
06guild_07	mapflag	nopenalty
06guild_08	mapflag	nopenalty